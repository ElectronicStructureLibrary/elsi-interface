### Generate version info ###
ADD_CUSTOM_COMMAND(OUTPUT ${PROJECT_BINARY_DIR}/generated/elsi_version.f90
  COMMAND perl ${PROJECT_SOURCE_DIR}/src/elsi_version_writer.pl ${elsi_DATESTAMP} > ${PROJECT_BINARY_DIR}/generated/elsi_version.f90
  VERBATIM
)

### Source files ###
LIST(APPEND elsi_src
  ${PROJECT_BINARY_DIR}/generated/elsi_version.f90
  elsi_c_interface.f90
  elsi_c2f.f90
  elsi_constants.f90
  elsi_datatype.f90
  elsi_dmp.f90
  elsi_interface.f90
  elsi_io.f90
  elsi_json.f90
  elsi_lapack.f90
  elsi_malloc.f90
  elsi_mat_io.f90
  elsi_mat_redist.f90
  elsi_mpi.f90
  elsi_mutator.f90
  elsi_occ.f90
  elsi_omm.f90
  elsi_pexsi.f90
  elsi_precision.f90
  elsi_setup.f90
  elsi_sips.f90
  elsi_solver.f90
  elsi_sort.f90
  elsi_utils.f90
)

SET_SOURCE_FILES_PROPERTIES(${PROJECT_BINARY_DIR}/generated/elsi_version.f90
  PROPERTIES GENERATED TRUE)

IF(NOT ENABLE_PEXSI)
  LIST(APPEND elsi_src stub_pexsi.f90)
ENDIF()

IF(NOT ENABLE_SIPS)
  LIST(APPEND elsi_src stub_sips.f90)
ENDIF()

IF(NOT ELPA_FOUND)
  LIST(APPEND elsi_src
    elsi_elpa.f90
    stub_aeo.f90
  )
ELSE()
  LIST(APPEND elsi_src elsi_aeo.f90)
ENDIF()

ADD_LIBRARY(elsi ${elsi_src})

IF(NOT ELPA_FOUND)
  ADD_DEPENDENCIES(elsi elpa)
ENDIF()

IF(NOT OMM_FOUND)
  ADD_DEPENDENCIES(elsi OMM)
ENDIF()

IF(NOT FORTJSON_FOUND)
  ADD_DEPENDENCIES(elsi fortjson)
ENDIF()

IF(ENABLE_PEXSI)
  ADD_DEPENDENCIES(elsi pexsi)
ENDIF()

IF(ENABLE_SIPS)
  ADD_DEPENDENCIES(elsi sips)
ENDIF()

IF(NOT OMM_FOUND)
  TARGET_LINK_LIBRARIES(elsi PRIVATE OMM MatrixSwitch)
ELSE()
  TARGET_LINK_LIBRARIES(elsi PRIVATE ${OMM_LIB})
  TARGET_INCLUDE_DIRECTORIES(elsi PRIVATE ${OMM_INC})
ENDIF()
IF(NOT ELPA_FOUND)
  TARGET_LINK_LIBRARIES(elsi PRIVATE elpa)
ELSE()
  TARGET_LINK_LIBRARIES(elsi PRIVATE ${ELPA_LIB})
  TARGET_INCLUDE_DIRECTORIES(elsi PRIVATE ${ELPA_INC})
ENDIF()
IF(NOT FORTJSON_FOUND)
  TARGET_LINK_LIBRARIES(elsi PRIVATE fortjson)
ELSE()
  TARGET_LINK_LIBRARIES(elsi PRIVATE ${FORTJSON_LIB})
  TARGET_INCLUDE_DIRECTORIES(elsi PRIVATE ${FORTJSON_INC})
ENDIF()
IF(ENABLE_SIPS)
  TARGET_LINK_LIBRARIES(elsi PRIVATE sips ${SLEPC_LIB})
  TARGET_INCLUDE_DIRECTORIES(elsi PRIVATE ${SLEPC_INC})
ENDIF()
IF(ENABLE_PEXSI)
  TARGET_LINK_LIBRARIES(elsi PRIVATE pexsi)
  IF(NOT SUPERLU_FOUND)
    TARGET_LINK_LIBRARIES(elsi PRIVATE superlu_dist)
  ELSE()
    TARGET_LINK_LIBRARIES(elsi PRIVATE ${SUPERLU_LIB})
  ENDIF()
  IF(NOT PTSCOTCH_FOUND)
    TARGET_LINK_LIBRARIES(elsi PRIVATE
      ptscotchparmetis
      ptscotch
      ptscotcherr
      scotchmetis
      scotch
      scotcherr
    )
  ELSE()
    TARGET_LINK_LIBRARIES(elsi PRIVATE ${PTSCOTCH_LIB})
  ENDIF()
ENDIF()
TARGET_LINK_LIBRARIES(elsi PRIVATE ${MATH_LIB})
IF(ENABLE_MKL)
  SET_TARGET_PROPERTIES(elsi
    PROPERTIES LINK_FLAGS "-mkl=cluster")
ENDIF()

### Installation ###
IF(NOT "${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR}" STREQUAL "${PROJECT_BINARY_DIR}/lib")
  INSTALL(TARGETS elsi
    DESTINATION ${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR})
ENDIF()

IF(NOT "${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_INCLUDEDIR}" STREQUAL "${PROJECT_BINARY_DIR}/include")
  INSTALL(DIRECTORY ${CMAKE_Fortran_MODULE_DIRECTORY}/
    DESTINATION ${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_INCLUDEDIR}
    PATTERN "*.mod")
ENDIF()

INSTALL(FILES elsi.h
  DESTINATION ${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_INCLUDEDIR})
