### CMake version ###
CMAKE_MINIMUM_REQUIRED(VERSION 3.0 FATAL_ERROR)

### Project ###
PROJECT(elsi LANGUAGES NONE)
SET(elsi_URL "http://elsi-interchange.org")
SET(elsi_EMAIL "elsi-team@duke.edu")
SET(elsi_LICENSE "BSD 3")
SET(elsi_DESCRIPTION "Electronic Structure Infrastructure")
SET(elsi_DATESTAMP "180513")

### CMake modules ###
LIST(APPEND CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/cmake)
INCLUDE(color_message)
INCLUDE(external_libraries)

### Installation paths ###
IF(PROJECT_BINARY_DIR STREQUAL PROJECT_SOURCE_DIR)
  MESSAGE(FATAL_ERROR "${MAGENTA}Build in the source directory is not allowed${COLORRESET}")
ENDIF()
SET(CMAKE_Fortran_MODULE_DIRECTORY "${PROJECT_BINARY_DIR}/include")
SET(CMAKE_LIBRARY_OUTPUT_DIRECTORY "${PROJECT_BINARY_DIR}/lib")
SET(CMAKE_ARCHIVE_OUTPUT_DIRECTORY "${PROJECT_BINARY_DIR}/lib")
SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${PROJECT_BINARY_DIR}/bin")
FILE(MAKE_DIRECTORY "${PROJECT_BINARY_DIR}/generated")
IF(NOT DEFINED CMAKE_INSTALL_INCLUDEDIR)
  SET(CMAKE_INSTALL_INCLUDEDIR "include" CACHE PATH "Location to install module and header files")
ENDIF()
IF(NOT DEFINED CMAKE_INSTALL_LIBDIR)
  SET(CMAKE_INSTALL_LIBDIR "lib" CACHE PATH "Location to install library files")
ENDIF()
IF(NOT DEFINED CMAKE_INSTALL_BINDIR)
  SET(CMAKE_INSTALL_BINDIR "bin" CACHE PATH "Location to install binary files")
ENDIF()
MESSAGE(STATUS "ELSI will be installed to: ${CMAKE_INSTALL_PREFIX}")

### Options ###
OPTION(ENABLE_TESTS "Build ELSI Fortran test programs" OFF)
OPTION(ENABLE_C_TESTS "Build ELSI C test programs" OFF)
OPTION(ENABLE_PEXSI "Enable PEXSI" OFF)
OPTION(ENABLE_SIPS "Enable SLEPc-SIPs" OFF)
OPTION(USE_EXTERNAL_ELPA "Use external ELPA" OFF)
OPTION(USE_EXTERNAL_OMM "Use external libOMM" OFF)
OPTION(USE_EXTERNAL_SUPERLU "Use external SuperLU_DIST" OFF)
OPTION(ADD_UNDERSCORE "Suffix C functions with an underscore" ON)
SET(ELPA2_KERNEL "" CACHE STRING "Use AVX/AVX2/AVX512/BGQ ELPA2 kernel")
SET(MPIEXEC "mpiexec" CACHE STRING "Name of MPI job launcher")
SET(MPISIZE "4" CACHE STRING "Number of MPI tasks to run tests")
SET(LIB_PATH "" CACHE STRING "Directories containing libraries to be linked against")
SET(INC_PATH "" CACHE STRING "Include directories")
SET(ELPA_LIB "" CACHE STRING "External ELPA libraries")
SET(OMM_LIB "" CACHE STRING "External libOMM libraries")
SET(SUPERLU_LIB "" CACHE STRING "External SuperLU_DIST libraries")
SET(SLEPC_LIB "" CACHE STRING "External SLEPc libraries")
SET(MATH_LIB "" CACHE STRING "External Linear algebra libraries")

### Compilers ###
IF(NOT DEFINED CMAKE_Fortran_COMPILER)
  MESSAGE(FATAL_ERROR "${MAGENTA}Fortran compiler must be set${COLORRESET}")
ELSE()
  MESSAGE(STATUS "${GREEN}Fortran compiler${COLORRESET}: ${CMAKE_Fortran_COMPILER}")
  ENABLE_LANGUAGE(Fortran)
ENDIF()
IF(NOT DEFINED CMAKE_C_COMPILER)
  MESSAGE(FATAL_ERROR "${MAGENTA}C compiler must be set${COLORRESET}")
ELSE()
  MESSAGE(STATUS "${GREEN}C compiler${COLORRESET}: ${CMAKE_C_COMPILER}")
  ENABLE_LANGUAGE(C)
ENDIF()
IF(ENABLE_PEXSI)
  IF(NOT DEFINED CMAKE_CXX_COMPILER)
    MESSAGE(FATAL_ERROR "${MAGENTA}C++ compiler must be set${COLORRESET}")
  ELSE()
    MESSAGE(STATUS "${GREEN}C++ compiler${COLORRESET}: ${CMAKE_CXX_COMPILER}")
    ENABLE_LANGUAGE(CXX)
  ENDIF()
ENDIF()

### External libraries ###
FOREACH(str ${ELPA_LIB} ${OMM_LIB} ${SUPERLU_LIB} ${SLEPC_LIB} ${MATH_LIB})
  STRING(REPLACE "-l" "" ${str} "${str}")
ENDFOREACH()
FOREACH(str ELPA_LIB OMM_LIB SUPERLU_LIB SLEPC_LIB MATH_LIB LIB_PATH INC_PATH) 
  convert_to_list(${str})
ENDFOREACH()
FOREACH(libs ELPA_LIB OMM_LIB SUPERLU_LIB SLEPC_LIB MATH_LIB)
  generate_library_targets(LIB_PATH ${libs})
ENDFOREACH()

### Compilations ###
INCLUDE_DIRECTORIES(${CMAKE_Fortran_MODULE_DIRECTORY})

IF(USE_EXTERNAL_ELPA)
  MESSAGE(STATUS "Using external ELPA")
ELSE()
  MESSAGE(STATUS "Enabling internal ELPA")
  ADD_SUBDIRECTORY(external/ELPA)
ENDIF()

IF(USE_EXTERNAL_OMM)
  MESSAGE(STATUS "Using external libOMM")
ELSE()
  MESSAGE(STATUS "Enabling internal libOMM")
  ADD_SUBDIRECTORY(external/libOMM)
ENDIF()

IF(ENABLE_PEXSI)
  MESSAGE(STATUS "Enabling internal PEXSI")
  ADD_SUBDIRECTORY(external/PEXSI)

  IF(USE_EXTERNAL_SUPERLU)
    MESSAGE(STATUS "Using external SuperLU_DIST")

    SET(USE_EXTERNAL_PTSCOTCH OFF CACHE BOOL "Use external PT-SCOTCH" FORCE)
  ELSE()
    MESSAGE(STATUS "Enabling internal SuperLU_DIST")
    ADD_SUBDIRECTORY(external/SuperLU_DIST)

    IF(USE_EXTERNAL_PTSCOTCH)
      MESSAGE(STATUS "Using external PT-SCOTCH")
    ELSE()
      MESSAGE(STATUS "Enabling internal PT-SCOTCH")
      ADD_SUBDIRECTORY(external/SCOTCH)
    ENDIF()
  ENDIF()
ENDIF()

IF(ENABLE_SIPS)
  MESSAGE(STATUS "Enabling SLEPc-SIPs")
  ADD_SUBDIRECTORY(external/SIPS)
ENDIF()

ADD_SUBDIRECTORY(external/FortJSON)
ADD_SUBDIRECTORY(src)

IF(ENABLE_TESTS OR ENABLE_C_TESTS)
  MESSAGE(STATUS "Enabling test programs")
  ENABLE_TESTING()
  ADD_SUBDIRECTORY(test)
ENDIF()
